buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+', changing: true
    }
}

plugins {
    id "org.sonarqube" version "3.3"
    id "maven-publish"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'idea'

repositories {
    mavenCentral()
    maven { url "https://dvs1.progwml6.com/files/maven" }
    maven { url "https://maven.theillusivec4.top/" }
    maven { url "https://maven.tterrag.com/" }
    maven { url = "https://modmaven.dev/" }
    maven { url "https://minecraft.curseforge.com/api/maven/"}
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url "https://maven.blamejared.com/" }
}

group = 'sophisticatedbackpacks'
version = "${project.minecraft_version}-${project.mod_version}.${getBuildNumber()}${getStable()}"
archivesBaseName = 'sophisticatedbackpacks'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

minecraft {
    mappings channel: 'parchment', version: "$parchment_version"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        all {
            //TODO remove this once PR https://github.com/MinecraftForge/MinecraftForge/pull/8064 gets merged
            property 'ignoreList', "bootstraplauncher,securejarhandler,asm-commons,asm-util,asm-analysis,asm-tree,asm,client-extra,fmlcore,javafmllanguage,mclanguage,forge-${project.minecraft_version}-${project.forge_version},${project.minecraft_version}-forge-${project.forge_version}"
        }
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property "mixin.env.disableRefMap", "true"
            //property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                sophisticatedbackpacks {
                    source sourceSets.main
                }
            }
        }
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property "mixin.env.disableRefMap", "true"
            //property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            workingDirectory project.file('run')
            mods {
                sophisticatedbackpacks {
                    source sourceSets.main
                }
            }
        }
        data {
            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property "mixin.env.disableRefMap", "true"
            //property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            workingDirectory project.file('run')
            mods {
                sophisticatedbackpacks {
                    source sourceSets.main
                }
            }
            args '--mod', 'sophisticatedbackpacks,sophisticatedcore', '--flat', '--all', '--output', file('src/generated/resources/')
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    //compileOnly fg.deobf("vazkii.botania:Botania:${botania_version}:api")
    //runtimeOnly fg.deobf("vazkii.botania:Botania:${botania_version}")
    //compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")
    //runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    compileOnly fg.deobf("curse.maven:craftingtweaks-233071:${crafting_tweaks_cf_file_id}")
    compileOnly fg.deobf("curse.maven:balm-531761:${balm_cf_file_id}")
    runtimeOnly fg.deobf("curse.maven:craftingtweaks-233071:${crafting_tweaks_cf_file_id}")
    runtimeOnly fg.deobf("curse.maven:balm-531761:${balm_cf_file_id}")

    //runtimeOnly fg.deobf("slimeknights.mantle:Mantle:1.16.5-1.6.115")
    //runtimeOnly fg.deobf("slimeknights.tconstruct:TConstruct:1.16.5-3.1.0.242")
    //runtimeOnly fg.deobf("curse.maven:mgui-351914:3291267")
    //runtimeOnly fg.deobf("curse.maven:tetra-289712:3371803")
    //runtimeOnly fg.deobf("curse.maven:invtweaks-383070:3102237")
    //runtimeOnly fg.deobf("curse.maven:mekanism-268560:3206392")
}

test {
    useTestNG()
}

task generateSources(type: Copy) {
    from 'src/replaceme/java'
    into "$buildDir/generated-src"
    filter { line -> line.replaceAll('@MOD_VERSION@', '${project.version}') }
}

jar {
    from('/') {
        include 'LICENSE'
    }

    manifest {
        attributes(["Specification-Title"     : "sophisticatedbackpacks",
                    "Specification-Vendor"    : "p3pp3rf1y",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "p3pp3rf1y",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

processResources {
    // Exclude datagenerator .cache directory
    exclude '.cache'
}

static def getBuildNumber() {
    if (System.getenv("GITHUB_RUN_NUMBER") != null) {
        return System.getenv("GITHUB_RUN_NUMBER").toString()
    }
    return ""
}

static def getStable() {
    if ((System.getenv("GITHUB_REF") == null || System.getenv("GITHUB_REF").endsWith("-dev"))) {
        return "-alpha"
    }
    return ""
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/P3pp3rF1y/SophisticatedBackpacks")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId = 'sophisticatedbackpacks'
            from(components.java)
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "SophisticatedBackpacks"
        property "sonar.projectKey", "sophisticatedbackpacks:SophisticatedBackpacks"
    }
}

